apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

def getEnviromentVariable(String variableName, int defaultValue) {
    String environmentVariable = System.getenv(variableName)
    if (environmentVariable != null) {
        return Integer.parseInt(environmentVariable)
    } else {
        return defaultValue
    }
}

def getVersionBuild() {
    return getEnviromentVariable('BUILD_NUMBER', 1)
}

android {

    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 31
        versionCode versionMajor * 100000000 + versionMinor * 1000000 + versionPatch * 10000 + getVersionBuild()
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        archivesBaseName = "ParrotBot-$versionName"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildTypes {
        debug {
            versionNameSuffix "-dev1"

            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false

            tasks.withType(Test) { enabled = false }
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation Kotlin.stdLib
    implementation Kotlin.coreKtx

    implementation AndroidX.appCompat

    implementation DI.coreKoin
    implementation DI.androidKoin

    implementation Log.timber

    implementation Concurrency.coroutines
}
